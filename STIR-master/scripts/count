#! /bin/csh -fe
# A script to write a sequence of numbered strings to stdout
#
#  Copyright (C) 2005- 2008, Hammersmith Imanet Ltd
#  This file is part of STIR.
#
#  This file is free software; you can redistribute it and/or modify
#  it under the terms of the GNU Lesser General Public License as published by
#  the Free Software Foundation; either version 2.1 of the License, or
#  (at your option) any later version.

#  This file is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU Lesser General Public License for more details.
#
#  See STIR/LICENSE.txt for details
#      
# Author Kris Thielemans
# Examples:
# count 3 5
# => 3 4 5
# count 3 9 2
# => 3 5 7 9
# count -pre A --post B 3 5
# => A3B A4B A5B

# option processing
set prefix=
set postfix=
# check if more than 2 args and first arg starts with a -
while ( $#argv > 2 && "$1" =~ -* )

switch ("$1")
case --pre:
    set prefix = "$2"
    shift
    shift
    breaksw

case --post:
    set postfix = "$2"
    shift
    shift
    breaksw

default:
    echo count: Unrecognised option "$1"
    exit 1

endsw
end

if ($#argv != 2 && $#argv != 3) then
  echo "usage: count [--pre prefixtext] [--post postfixtest] minimum maximum [increment]"
  exit 1
endif

if ($#argv == 3) then
    set increment = $3
else
    set increment = 1
endif

set all =
set frame = $1
while ($frame <= $2 )
  set all = "$all $prefix$frame$postfix"
  @ frame+= $increment
end
echo $all
